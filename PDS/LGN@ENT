         MACRO
.* ------------------------------------------*
.* Entry linkage for most modules            *
.*                                           *
.* 07/2000 axb allow for worklen > 4096      *
.*                                           *
.*                                           *
.* 10/2000 axb extra parms for s/routine call*
.*                                           *
.* ------------------------------------------*
&LABEL   LGN@ENT  &WHAT,&WORK=WORKAREA,&WORKL=WORKLEN
         AIF ('&WHAT' EQ 'CSECT').CSECT
         AIF ('&WHAT' EQ 'ENTRY').ENTRY
         MNOTE 16,'Incorrect LGN@ENT parameter'
         MEXIT
.CSECT   ANOP
&LABEL   CSECT
         AGO   .COMMON
.ENTRY   ANOP
         ENTRY  &LABEL
&LABEL   DS    0H
         AGO   .COMMON
.COMMON  ANOP
         USING &LABEL,R15                ; Address program.
         SAVE  (14,12),,&LABEL..SSE.ALEX.BRODSKY.&SYSDATE.&SYSTIME
         LR    R12,R15            ; Set program base.
         DROP  R15
         USING &LABEL,R12,R11         ; Address program.
         LA    R11,2048(R12)
         LA    R11,2048(R11)
* 'STORAGE OBTAIN,LENGTH=&WORKL,ADDR=(R1),LOC=BELOW,COND=NO'
         STORAGE OBTAIN,LENGTH=&WORKL,ADDR=(R1),LOC=BELOW,COND=NO
         LA    R6,0(R1)           ; Target addr
         L     R7,=A(&WORKL)      ; Target size
         LR    R8,R6              ; source=target
         SR    R9,R9              ; clear area
         MVCL  R6,R8              ; ...
         ST    R1,8(R13)          ; Chain saveareas.
         ST    R13,4(R1)          ; ...
         LR    R13,R1             ; ...
         USING &WORK,R13
****     XC    RETCODE(4),RETCODE ; Set normal return code - 0
         L     R1,4(,R13)         ; Get to the prev savearea
         LM    R0,R10,20(R1)      ; Restore entry registers
         MEND
